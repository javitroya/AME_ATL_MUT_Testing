module CPL2SPL;
create OUT : SPL from IN : CPL;

-- We consider nodes are statements, by default.
helper context CPL!Node def: statement : CPL!Node =
	self;

-- The "location" node is not a statement.
helper context CPL!Location def: statement : CPL!Node =
	self.contents.statement;

-- @begin Locations computation
-- By default, locations of an element are locations of its container.
helper context CPL!Element def: locations : Sequence(CPL!Location) =
	self.refImmediateComposite().locations;

-- Locations of the CPL root is the empty sequence.
helper context CPL!CPL def: locations : Sequence(CPL!Location) =
	Sequence {};

-- @see last paragraph of 6.1 just before 6.1.1 of CPL RFC
helper context CPL!Location def: locations : Sequence(CPL!Location) =
	Sequence {self}->union(self.refImmediateComposite().locations);

--helper context CPL!Redirection def: locations : Sequence(CPL!Location) =
--	Sequence {self}->union(self.refImmediateComposite().locations);
-- @end Locations computation

rule CPL2Program { --R1
	from
		s : CPL!CPL
	to
		t : SPL!Program (
			service <- service
		),
		service : SPL!Service (
			name <- 'unnamed',
			declarations <- s.subActions,
			sessions <- dialog
		),
		dialog : SPL!Dialog (
			methods <- Sequence {s.incoming, s.outgoing}
		)
}

rule SubAction2Function { --R2
	from
		s : CPL!SubAction
	to
		t : SPL!LocalFunctionDeclaration (
			name <- s.id,
			returnType <- rt,
			statements <- s.contents.statement
		),
		rt : SPL!SimpleType (
			type <- #response
		)
}


rule Incoming2Method { --R3
	from
		s : CPL!Incoming
	to
		t : SPL!Method (
			type <- rt,
			direction <- #"inn",
			methodName <- mn,
			statements <-
				if s.contents.oclIsUndefined() then
					Sequence {}
				else
					Sequence {s.contents.statement}
				endif
		),
		rt : SPL!SimpleType (
			type <- #response
		),
		mn : SPL!SIPMethodName (
			name <- #INVITE
		)
}

helper context CPL!Proxy def: isSimple : Boolean =
	self.busy.oclIsUndefined() and
	self.noAnswer.oclIsUndefined() and
	self.redirection.oclIsUndefined() and
	self.failure.oclIsUndefined() and
	self.default.oclIsUndefined();

rule Proxy2Return { --R4
	from
		s : CPL!Proxy (
			s.isSimple
		)
	to
		t : SPL!ReturnStat (
			returnedValue <- fwd
		),
		fwd : SPL!ForwardExp (
			isParallel <- s.ordering = 'parallel',
			exp <- exp
		),
		exp : SPL!ConstantExp (
			value <- thisModule.Location2URIConstant(s.locations->first())
		)
}

-- @begin Complex Proxy
rule Proxy2Select { --R5
	from
		s : CPL!Proxy (
			not s.isSimple
		)
	to
		t : SPL!CompoundStat ( --cs
			statements <- Sequence {declStat, select}
		),

		-- response r = [parallel] forward <uri>;
		declStat : SPL!DeclarationStat ( --ds
			declaration <- decl
		),
		decl : SPL!VariableDeclaration ( --vd
			type <- rt,
			name <- 'r',
			initExp <- fwd
		),
		rt : SPL!SimpleType ( --st
			type <- #response
		),
		fwd : SPL!ForwardExp ( -- no poner
			isParallel <- s.ordering = 'parallel',
			exp <- exp
		),
		exp : SPL!ConstantExp ( -- no poner
			value <- thisModule.Location2URIConstant(s.locations->first())
		),

		-- select(r)
		select : SPL!SelectStat ( --ss
			matchedExp <- v,
			selectCases <- Sequence {s.busy, s.noAnswer, s.redirection, s.failure},
			selectDefault <- s.default
		),
		v : SPL!Variable ( --v
			source <- decl
		)
}

rule Busy2SelectCase { --R6
	from
		s : CPL!Busy
	to
		t : SPL!SelectCase (
			commentsBefore <- Sequence {'// busy'},
			values <- Sequence {v},
			statements <- Sequence {s.contents.statement}
		),
		v : SPL!ResponseConstant (
			response <- r
		),
		r : SPL!ClientErrorResponse (
			errorKind <- #BUSY_HERE
		)
}

rule NoAnswer2SelectCase { --R7
	from
		s : CPL!NoAnswer
	to
		t : SPL!SelectCase (
			commentsBefore <- Sequence {'// noanswer'},
			values <- Sequence {v},
			statements <- Sequence {s.contents.statement}
		),
		v : SPL!ResponseConstant (
			response <- r
		),
		r : SPL!ClientErrorResponse (
			errorKind <- #REQUEST_TIMEOUT
		)
}

rule Redirection2SelectCase { --R8
	from
		s : CPL!Redirection
	to
		t : SPL!SelectCase (
			commentsBefore <- Sequence {'// redirection'},
			values <- Sequence {v},
			statements <- Sequence {s.contents.statement}
		),
		v : SPL!ResponseConstant (
			response <- r
		),
		r : SPL!RedirectionErrorResponse (
			errorKind <- OclUndefined
		)
}

rule Default2SelectDefault { --R9
	from
		s : CPL!Default
	to
		t : SPL!SelectDefault (
			commentsBefore <- Sequence {'// default'},
			statements <- Sequence {s.contents.statement}
		)
}
-- @end Complex Proxy

rule SubCall2Return { --R10
	from
		s : CPL!SubCall
	to
		t : SPL!ReturnStat (
			returnedValue <- callExp
		),
		callExp : SPL!FunctionCallExp (
			functionCall <- call
		),
		call : SPL!FunctionCall (
			function <- CPL!SubAction.allInstancesFrom('IN')->select(e | e.id = s.ref)->first()
		)
}

rule Redirect2Return { --R11
	from
		s : CPL!Redirect
	to
		t : SPL!ReturnStat ( --rs
			returnedValue <- withExp
		),
		withExp : SPL!WithExp ( --we
			exp <- v,
			msgFields <- Sequence {reason, contact}
		),
		v : SPL!ConstantExp ( --cep
			value <- rc
		),
		rc : SPL!ResponseConstant ( --rc
			response <- rer
		),
		rer : SPL!RedirectionErrorResponse ( -- rer
			errorKind <- if s.permanent = 'true' then #MOVED_PERMANENTLY else #MOVED_TEMPORARILY endif
		),

		reason : SPL!ReasonMessageField ( --rmf
			exp <- reasonConstant
		),
		reasonConstant : SPL!ConstantExp ( --cep2
			value <- reasonString
		),
		reasonString : SPL!StringConstant ( --sc
			value <- 'unspecified'
		),

		contact : SPL!HeadedMessageField ( -- hmf
			headerId <- '#contact:',
			exp <- contactConstant
		),
		contactConstant : SPL!ConstantExp ( --ce
			value <- thisModule.Location2URIConstant(s.locations->first())
		)
}

-- @begin Switches
rule AddressSwitch2SelectStat { --R12
	from
		s : CPL!AddressSwitch --as
	to
		t : SPL!SelectStat ( --ss
			matchedExp <- v,
			selectCases <- s.addresses->including(s.notPresent),
			selectDefault <- s.otherwise
		),
		v : SPL!SIPHeaderPlace ( --shp
			header <-	if s.field = 'origin' then
						#FROM
					else -- s.field = 'destination' or s.field = 'original-destination'
						#TO
					endif
		)
}

rule SwitchedAddress2SelectCase { --R13
	from
		s : CPL!SwitchedAddress
	to
		t : SPL!SelectCase (
			values <- Sequence {v},
			statements <- Sequence {s.contents.statement}
		),
		v : SPL!StringConstant (
			value <- s.is
		)
}

rule Otherwise2SelectDefault { --R14
	from
		s : CPL!Otherwise --o
	to
		t : SPL!SelectDefault ( --sd
			statements <- s.contents.statement
		)
}
-- @end Switches

lazy rule Location2URIConstant { --R15
	from
		s : CPL!Location
	to
		t : SPL!URIConstant (
			uri <- s.url
		)
}

